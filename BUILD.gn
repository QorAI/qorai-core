# Copyright (c) 2017 The QorAI Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import(
    "//qorai/browser/qorai_vpn/win/qorai_vpn_wireguard_service/allowlist.gni")
import("//qorai/build/config.gni")
import("//qorai/build/redirect_cc.gni")
import("//qorai/components/qorai_vpn/common/buildflags/buildflags.gni")
import("//qorai/tools/crates/config.gni")
import("//build/config/locales.gni")
import("//build/config/zip.gni")
import("//chrome/browser/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")
import("//media/media_options.gni")
import("//third_party/icu/config.gni")
import("//third_party/widevine/cdm/widevine.gni")
import("//tools/grit/repack.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")
import("//ui/base/ui_features.gni")

if (enable_library_cdms) {
  import("//media/cdm/library_cdm/cdm_paths.gni")
}

if (!is_ios) {
  import("//qorai/qorai_paks.gni")
}

if (is_mac) {
  import("//qorai/build/mac/tweak_info_plist.gni")
  import("//build/config/mac/rules.gni")
}

if (is_win) {
  import("//qorai/build/win/sign.gni")
}

if (is_linux) {
  import("//qorai/build/linux/channels.gni")
}

declare_args() {
  audit_dev_deps = true
}

chromium_unit_tests_deps = [
  "//base:base_unittests",
  "//net:net_unittests",
]
if (!is_ios) {
  chromium_unit_tests_deps += [ "//chrome/test:unit_tests" ]
}
if (use_blink && !is_android) {
  # TODO(https://github.com/qorai/qorai-browser/issues/48062): Fix this target
  # for android.
  chromium_unit_tests_deps += [ "//content/test:content_unittests" ]
}

if (!is_android) {
  # TODO(https://github.com/qorai/qorai-browser/issues/48062): Fix this target
  # for android.
  chromium_unit_tests_deps += [ "//components:components_unittests" ]
}

group("chromium_unit_tests") {
  testonly = true
  data_deps = chromium_unit_tests_deps
  write_file("$root_out_dir/chromium_unit_tests.json",
             chromium_unit_tests_deps,
             "json")
}

# Only add tests to this list if you want them to run by default for
# qorai_all_unit_tests.
qorai_all_unit_tests_deps = [ "//qorai/components:qorai_components_unittests" ]
if (!is_ios) {
  qorai_all_unit_tests_deps += [ "//qorai/test:qorai_unit_tests" ]
} else {
  qorai_all_unit_tests_deps += [
    "//qorai/ios/testing:ios_qorai_unit_tests",
    "//qorai/ios/web:ios_qorai_web_inttests",
    "//qorai/ios/web:ios_qorai_web_unittests",
  ]
}
if (!is_ios && !is_android) {
  qorai_all_unit_tests_deps += [ "//qorai/test:qorai_installer_unittests" ]
}
if (is_win || is_mac || is_linux) {
  qorai_all_unit_tests_deps += [ "//qorai/updater:qorai_updater_unittests" ]
}

# This group is for unit tests that run as part of qorai_all_unit_tests. Other
# tests that you only want to build should be added directly to `all` group
group("qorai_all_unit_tests") {
  testonly = true
  data_deps = qorai_all_unit_tests_deps
  write_file("$root_out_dir/qorai_all_unit_tests.json",
             qorai_all_unit_tests_deps,
             "json")
}

# This target serves as a canonical place to define all code that has to be
# buildable in all platforms in a single build run.
group("all") {
  testonly = true
  deps = [
    ":qorai_all_unit_tests",
    ":chromium_unit_tests",
    "//qorai",
  ]

  if (!is_android && !is_ios) {
    deps += [
      "//qorai/test:qorai_network_audit_tests",
      "//chrome/test:browser_tests",
    ]
  }

  if (toolkit_views) {
    deps += [ "//ui/views/examples:views_examples" ]
  }

  if (!is_ios) {
    deps += [ "//qorai/test:qorai_browser_tests" ]
  }

  if (is_android) {
    deps += [
      "//qorai/build/android:qorai",
      "//qorai/test:qorai_java_unit_tests",
      "//qorai/test:qorai_junit_tests",
    ]
  }
}

if (!is_ios) {
  group("child_dependencies") {
    deps = [
      "common",
      "//qorai/renderer",
      "//qorai/utility",
    ]
  }

  group("browser_dependencies") {
    deps = [
      "browser",
      "chromium_src/chrome/app",
      "common",
      "//components/omnibox/common",
      "//services/device/public/cpp:device_features",
      "//services/network/public/cpp",
    ]

    data_deps = [ ":unpacked_resources" ]

    if (!is_ios) {
      deps += [ ":packed_resources" ]
    }
  }

  group("storybook") {
    deps = [ "//qorai/.storybook:storybook" ]
  }
}

if (is_win) {
  packed_files_rc_file =
      "$root_out_dir/gen/chrome/installer/mini_installer/packed_delta_files.rc"
  action("delta_installer_archive") {
    script = "//qorai/tools/win/create_delta_archive.py"
    if (skip_signing) {
      setup_exe = "setup.exe"
    } else {
      setup_exe = "presigned_binaries/setup.exe"
    }
    inputs = [
      # The original tool for delta archive generation was called "courgette".
      # But it runs out of memory on recent Chromium / QorAI revisions. Its
      # drop-in replacement is Zucchini. We need to use its 64-bit version to
      # again prevent out of memory errors.
      "$root_out_dir/zucchini64.exe",
      "$root_out_dir/chrome.7z",
      "$root_out_dir/" + setup_exe,
    ]
    outputs = [
      "$root_out_dir/chrome_patch.diff",
      "$root_out_dir/chrome_patch.packed.7z",
      "$root_out_dir/setup_patch.diff",
      "$root_out_dir/setup_patch.packed.7z",
      packed_files_rc_file,
    ]
    deps = [
      "//chrome/installer/mini_installer:mini_installer_archive",
      "//chrome/installer/setup",
      "//components/zucchini:zucchini64",
    ]
    if (!skip_signing) {
      deps += [ ":signed_setup_exe" ]
    }
    args = [
      "zucchini64.exe",
      "$last_chrome_installer/chrome.7z",
      "chrome.7z",
      "$last_chrome_installer/setup.exe",
      setup_exe,
      "chrome_patch.diff",
      "chrome_patch.packed.7z",
      "setup_patch.diff",
      "setup_patch.packed.7z",
      rebase_path(packed_files_rc_file, root_build_dir),
    ]
    if (is_component_build) {
      args += [ "--is_component_build" ]
    }
  }

  if (!skip_signing) {
    sign("signed_setup_exe") {
      sources = [ "$root_out_dir/setup.exe" ]
      outputs = [ "$root_out_dir/presigned_binaries/setup.exe" ]
      deps = [ "//chrome/installer/setup" ]
    }
    sign("signed_qorai_exe") {
      sources = [ "$root_out_dir/qorai.exe" ]
      outputs = [ "$root_out_dir/presigned_binaries/qorai.exe" ]
      deps = [ "//qorai/build/win:copy_exe" ]
    }
    sign("signed_chrome_dll") {
      sources = [ "$root_out_dir/chrome.dll" ]
      outputs = [ "$root_out_dir/presigned_binaries/chrome.dll" ]
      deps = [ "//chrome:chrome_dll" ]
    }
  }

  group("mini_installer_dependencies") {
    deps = [ ":qorai" ]
    if (!skip_signing) {
      deps += [
        ":signed_qorai_exe",
        ":signed_chrome_dll",
        ":signed_setup_exe",
      ]
      if (enable_widevine && enable_cdm_host_verification) {
        deps += [
          "//qorai/browser/widevine:qorai_exe_sig",
          "//qorai/browser/widevine:chrome_dll_sig",
        ]
      }
    }
  }
}

group("qorai") {
  deps = [ "build/$target_os:qorai" ]
  if (!is_android && !is_ios) {
    deps += [
      "//qorai/components/version_info:generate_version",
      "//chrome",
    ]
  }
}

group("resources") {
  public_deps = [
    # TODO(bridiver)
    # "//qorai/browser:resources",
    # "//qorai/common:resources",
    # "//qorai/renderer:resources",
    "//qorai/components/resources",
  ]

  data_deps = [ "//qorai/components/resources:about_credits" ]
}

if (is_android) {
  android_assets("qorai_file_assets") {
    sources = [
      "//qorai/LICENSE.html",
      "//qorai/android/java/org/chromium/chrome/browser/ntp/ntp_news_optin_icon_animation.json",
      "//qorai/android/java/org/chromium/chrome/browser/onboarding/animations/onboarding_ads.json",
      "//qorai/android/java/org/chromium/chrome/browser/onboarding/animations/onboarding_ads_notification.json",
      "//qorai/android/java/org/chromium/chrome/browser/onboarding/animations/onboarding_rewards.json",
      "//qorai/android/java/org/chromium/chrome/browser/onboarding/animations/privacy_protection.json",
      "//qorai/android/java/org/chromium/chrome/browser/onboarding/animations/save_data_and_battery.json",
      "//qorai/android/java/org/chromium/chrome/browser/onboarding/animations/website_loads_faster.json",
      "//qorai/android/java/org/chromium/chrome/browser/vpn/qorai_vpn_confirm.json",
    ]
    disable_compression = false
  }

  android_assets("qorai_pak_assets") {
    sources = [ "$root_out_dir/qorai_resources.pak" ]

    deps = [
      "//qorai:qorai_file_assets",
      "//qorai:packed_resources_extra",
    ]
    disable_compression = true
  }
}

if (is_mac) {
  group("framework_bundle_data") {
    deps = [ "components/qorai_extension/extension/qorai_extension:qorai_extension_framework_bundle_data" ]
  }
}

group("unpacked_resources") {
  if (!is_mac) {
    deps = []
    if (enable_extensions) {
      deps = [
        "//qorai/components/qorai_extension/extension/qorai_extension:locales",
      ]
    }
  }
}

#TODO(bridiver) - what to do
if (!is_ios) {
  group("qorai_tests") {
    testonly = true

    deps = [ "test:qorai_unit_tests" ]

    if (!is_android) {
      deps += [
        "test:qorai_browser_tests",
        "test:qorai_network_audit_tests",
      ]
    }
    if (use_libfuzzer) {
      deps += [ "//qorai/fuzzers:qorai_fuzzers" ]
    }
  }
}

if (!is_ios) {
  qorai_paks("packed_resources") {
    if (is_mac) {
      output_dir = "$root_gen_dir/repack"
      copy_data_to_bundle = true
    } else {
      output_dir = root_out_dir
    }
  }
}

branding_dir = "//chrome/app/theme/$branding_path_component"
branding_dir_100 =
    "//chrome/app/theme/default_100_percent/$branding_path_component"
copy("theme_files") {
  visibility = [ ":*" ]
  if (is_linux) {
    sources = [
      "$branding_dir/linux/product_logo_128_beta.png",
      "$branding_dir/linux/product_logo_128_dev.png",
      "$branding_dir/linux/product_logo_128_development.png",
      "$branding_dir/linux/product_logo_128_nightly.png",
      "$branding_dir/linux/product_logo_24_beta.png",
      "$branding_dir/linux/product_logo_24_dev.png",
      "$branding_dir/linux/product_logo_24_nightly.png",
      "$branding_dir/linux/product_logo_256_beta.png",
      "$branding_dir/linux/product_logo_256_dev.png",
      "$branding_dir/linux/product_logo_256_nightly.png",
      "$branding_dir/linux/product_logo_48_beta.png",
      "$branding_dir/linux/product_logo_48_dev.png",
      "$branding_dir/linux/product_logo_48_nightly.png",
      "$branding_dir/linux/product_logo_64_beta.png",
      "$branding_dir/linux/product_logo_64_dev.png",
      "$branding_dir/linux/product_logo_64_nightly.png",
      "$branding_dir_100/linux/product_logo_16_beta.png",
      "$branding_dir_100/linux/product_logo_16_dev.png",
      "$branding_dir_100/linux/product_logo_16_nightly.png",
      "$branding_dir_100/linux/product_logo_32_beta.png",
      "$branding_dir_100/linux/product_logo_32_dev.png",
      "$branding_dir_100/linux/product_logo_32_nightly.png",
    ]
  } else {
    sources = [
      "$branding_dir/product_logo_128_beta.png",
      "$branding_dir/product_logo_128_dev.png",
      "$branding_dir/product_logo_128_development.png",
      "$branding_dir/product_logo_128_nightly.png",
    ]
  }
  outputs = [ "$root_out_dir/installer/theme/{{source_file_part}}" ]
}

group("create_dist") {
  deps = []
  if (is_android) {
    if (android_aab_to_apk && target_cpu == "arm64") {
      deps += [ "build/android:sign_app_convert_aab_to_apk" ]
    } else {
      deps += [ "build/android:sign_app" ]
    }
  } else {
    deps += [ ":create_symbols_dist" ]
  }

  # for linux zip will be created along with installer
  if (!is_linux) {
    deps += [ ":create_dist_zips" ]
  }

  if (is_win) {
    deps += [
      "//qorai/build/win:create_signed_installer",
      "//qorai/components/policy:pack_policy_templates",
    ]

    if (build_omaha) {
      deps += [ "//qorai/vendor/omaha" ]
    }

    if (build_delta_installer) {
      deps += [ "//qorai/build/win:signed_delta_installer" ]
    }
  }
  if (is_mac) {
    deps += [ "//qorai/build/mac:create_dist_mac" ]
  }
  if (is_linux) {
    deps += [
      ":theme_files",
      "//qorai/app/linux:dist_resources",
      "//chrome/installer/linux:$linux_channel",
    ]
  }
}

if (!is_mac && !is_android && !is_ios) {
  copy("qorai_locale_dist_resources") {
    deps = [ "//chrome:packed_resources" ]

    sources = []
    foreach(locale, platform_pak_locales) {
      sources += [ "$root_out_dir/locales/$locale.pak" ]
    }
    outputs = [ "$qorai_dist_dir/locales/{{source_file_part}}" ]
  }

  if (enable_extensions) {
    group("qorai_extensions_locale_dist_resources") {
      deps = [
        "//qorai/components/qorai_extension/extension/qorai_extension:locales",
      ]

      public_deps = []
      foreach(locale, platform_pak_locales) {
        # public_deps is used intentionaly because ":create_dist_zip" needs the all dependency
        # of all locale files.
        public_deps += [ ":qorai_shields_locales_${locale}" ]
      }
    }

    foreach(locale, platform_pak_locales) {
      copy("qorai_shields_locales_${locale}") {
        deps = [ "//qorai/components/qorai_extension/extension/qorai_extension:locales_$locale" ]

        locale = string_replace(locale, "-", "_")
        sources = [ "$root_out_dir/resources/qorai_extension/_locales/$locale/messages.json" ]
        outputs = [ "$qorai_dist_dir/resources/qorai_extension/_locales/$locale/{{source_file_part}}" ]
      }
    }
  }
}

copy("qorai_license_dist_resources") {
  sources = [ "//qorai/LICENSE" ]

  outputs = [ "$qorai_dist_dir/{{source_file_part}}" ]
}

if (!is_android && !is_ios) {
  copy("qorai_dist_resources") {
    sources = [ "$root_out_dir/version" ]

    deps = [
      ":qorai",
      ":qorai_license_dist_resources",
      ":packed_resources",
      "build/$target_os:qorai",
      "//qorai/components/version_info:generate_version",
      "//chrome:packed_resources",
      "//third_party/icu:icudata",
    ]

    if (!is_mac) {
      sources += [
        "$root_out_dir/$qorai_exe",
        "$root_out_dir/qorai_100_percent.pak",
        "$root_out_dir/qorai_200_percent.pak",
        "$root_out_dir/qorai_resources.pak",
        "$root_out_dir/chrome_100_percent.pak",
        "$root_out_dir/chrome_200_percent.pak",
        "$root_out_dir/icudtl.dat",
        "$root_out_dir/resources.pak",
      ]

      if (v8_use_external_startup_data) {
        deps += [ "//v8" ]

        if (use_v8_context_snapshot) {
          sources += [ "$root_out_dir/v8_context_snapshot.bin" ]
          deps += [ "//tools/v8_context_snapshot" ]
        } else {
          sources += [ "$root_out_dir/snapshot_blob.bin" ]
        }
      }
    }

    outputs = [ "$qorai_dist_dir/{{source_file_part}}" ]
  }
}

if (target_cpu == "x86") {
  target_arch = "ia32"
} else {
  target_arch = target_cpu
}

template("create_dist_template") {
  action(target_name) {
    inputs = get_target_outputs(":qorai_license_dist_resources")

    file_inputs = []
    foreach(input, inputs) {
      file_inputs += [ rebase_path(input, qorai_dist_dir) ]
    }

    rebase_base_dir = rebase_path(qorai_dist_dir)

    outputs = [ invoker.output ]
    rebase_output = rebase_path(invoker.output)

    dir_inputs = invoker.dir_inputs

    script = "//qorai/script/create-dist.py"

    deps = [ ":qorai_license_dist_resources" ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    args = [
      "--base-dir=$rebase_base_dir",
      "--inputs=$file_inputs",
      "--dir-inputs=$dir_inputs",
      "--output=$rebase_output",
    ]
  }
}

if (should_generate_symbols) {
  create_dist_template("create_qorai_symbols_dist") {
    if (is_android) {
      output = "$qorai_dist_dir/$qorai_product_name-v$qorai_version-$qorai_platform-$target_android_base-$target_cpu-symbols-$target_android_output_format.zip"
    } else {
      output = "$qorai_dist_dir/$qorai_product_name-v$qorai_version-$qorai_platform-$target_arch-symbols.zip"
    }

    deps = [ "//qorai/app/$current_os:symbol_dist_resources" ]

    if (is_win) {
      dir_inputs = [ "$qorai_product_name.syms" ]
    } else {
      dir_inputs = [ "$qorai_product_name.breakpad.syms" ]
    }
  }

  if (is_mac) {
    copy("create_native_symbols_dist") {
      sources = [ "$root_out_dir/$chrome_product_full_name.dSYM.tar.bz2" ]
      outputs = [ "$qorai_dist_dir/$qorai_product_name-v$qorai_version-$qorai_platform-$target_arch-native-symbols.tar.bz2" ]

      deps = [ "//chrome:chrome_dsym_archive" ]
    }
  } else {
    group("create_native_symbols_dist") {
    }
  }
}

group("create_symbols_dist") {
  # Currently on some platforms (Android, Linux) this target generate more
  # than just symbols, so always depend on it.
  public_deps = [ "//qorai/app/$current_os:symbol_dist_resources" ]

  if (should_generate_symbols) {
    public_deps += [
      ":create_qorai_symbols_dist",
      ":create_native_symbols_dist",
    ]
  }
}

action("create_dist_zips") {
  output = "$qorai_dist_dir/$qorai_product_name-v$qorai_version-$qorai_platform-$target_arch.zip"
  outputs = [ output ]

  if (is_win) {
    # Repack a Chrome release archive to the QorAI format.
    # Besides changing from 7z to zip, the directory structure is a bit different.
    # TODO(atuchin): support other platforms.

    script = "//qorai/script/repack-archive.py"
    deps = [ "//chrome/installer/mini_installer:mini_installer_archive" ]

    input = "$root_out_dir/chrome.7z"
    inputs = [ input ]

    args = [
      "--input=" + rebase_path(input),
      "--output=" + rebase_path(output),
      "--target_dir=Chrome-bin",
    ]
  } else {  # !is_win
    script = "//qorai/script/create-dist.py"

    inputs = []
    deps = [ "app/$current_os:dist_resources" ]

    if (!is_mac) {
      inputs += get_target_outputs(":qorai_license_dist_resources")
      deps += [ ":qorai_license_dist_resources" ]
    }

    if (!is_mac && !is_android && !is_ios) {
      inputs += get_target_outputs(":qorai_dist_resources")
      inputs += get_target_outputs(":qorai_locale_dist_resources")
      if (enable_extensions) {
        foreach(locale, platform_pak_locales) {
          inputs += get_target_outputs(":qorai_shields_locales_${locale}")
        }
      }
    }

    file_inputs = []
    foreach(input, inputs) {
      file_inputs += [ rebase_path(input, qorai_dist_dir) ]
    }

    dir_inputs = []
    if (is_mac) {
      dir_inputs += [ qorai_exe ]
      rebase_base_dir = rebase_path(root_out_dir)
      deps += [ "//chrome:chrome_app" ]
    } else {
      rebase_base_dir = rebase_path(qorai_dist_dir, root_out_dir)
    }

    if (!is_mac && !is_android && !is_ios) {
      deps += [
        ":qorai_dist_resources",
        ":qorai_locale_dist_resources",
      ]

      if (enable_extensions) {
        deps += [ ":qorai_extensions_locale_dist_resources" ]
      }
    }

    rebase_output = rebase_path(output)
    args = [
      "--base-dir=$rebase_base_dir",
      "--inputs=$file_inputs",
      "--dir-inputs=$dir_inputs",
      "--output=$rebase_output",
    ]
  }  # !is_win
}

if (is_mac) {
  group("qorai_app") {
    deps = [ ":qorai_app_plist" ]
  }

  qorai_tweak_info_plist("qorai_app_plist") {
    info_plist = "$root_gen_dir/chrome/chrome_app_plist_tweaked.plist"

    args = [
      "--qorai_channel=" + qorai_channel,
      "--qorai_product_dir_name=" + qorai_product_dir_name,
      "--qorai_version=" + qorai_version,
      "--qorai_eddsa_key=" + sparkle_eddsa_public_key,
    ]

    if (skip_signing) {
      args += [ "--skip_signing" ]
    }

    if (enable_updater) {
      args += [ "--enable_updater" ]
    }

    deps = [ "//chrome:chrome_app_plist" ]
  }

  qorai_tweak_info_plist("qorai_helper_plist") {
    info_plist = "$root_gen_dir/chrome/chrome_helper_plist_tweaked.plist"

    args = [
      "--qorai_channel=" + qorai_channel,
      "--qorai_product_dir_name=" + qorai_product_dir_name,
      "--qorai_version=" + qorai_version,
    ]

    deps = [ "//chrome:chrome_helper_plist" ]
  }
}

action("audit_deps") {
  script = "//qorai/script/audit_deps.py"
  output = "$target_gen_dir/audit_deps.json"

  # Action must have outputs, and these outputs must be created, otherwise
  # SISO will fail.
  outputs = [ output ]

  args = [
    "--source_root=" + rebase_path("//qorai"),
    "--cargo_audit_exe=" + rebase_path(cargo_audit_exe),
    "--output=" + rebase_path(output),
  ]

  if (audit_dev_deps) {
    args += [ "--audit_dev_deps" ]
  }

  deps = [ "//qorai/tools/crates:build_cargo_audit($host_toolchain)" ]
}

if (is_redirect_cc_build) {
  group("redirect_cc") {
    deps = [ "//qorai/tools/redirect_cc" ]
  }
}
if (is_win && enable_resource_allowlist_generation) {
  # Merge chrome_resource_allowlist and wireguard resources allowlist to keep
  # wireguard resources in pak files.
  action("merge_allowlists") {
    deps = [
      "//qorai/browser/qorai_vpn/win/qorai_vpn_wireguard_service:wireguard_resource_allowlist",
      "//chrome:resource_allowlist",
    ]

    script = "//qorai//resources/qorai_allowlist.py"
    source_allowlist = "$root_gen_dir/chrome/chrome_resource_allowlist.txt"
    args = [
      "--repack_allowlist",
      rebase_path(source_allowlist, root_build_dir),
      "--qorai_allowlist",
      rebase_path(wireguard_resource_allowlist_file, root_build_dir),
      "--output",
      rebase_path(qorai_allowlist_file, root_build_dir),
    ]

    outputs = [ qorai_allowlist_file ]
  }
}
