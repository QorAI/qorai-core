# Copyright (c) 2019 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

if (is_android) {
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")
}

assert(!is_ios)

static_library("browser") {
  sources = [
    "qorai_referrals_service.cc",
    "qorai_referrals_service.h",
  ]

  public_deps = [ "//qorai/components/qorai_referrals/common" ]

  deps = [
    "//base",
    "//qorai/qorai_domains",
    "//qorai/components/qorai_stats/browser",
    "//qorai/components/constants",
    "//qorai/vendor/qorai_base",
    "//components/prefs",
    "//components/url_matcher",
    "//content/public/browser",
    "//net",
    "//services/network/public/cpp",
  ]

  if (is_mac) {
    sources += [
      "file_extended_attribute.cc",
      "file_extended_attribute.h",
    ]
  }

  if (is_android) {
    sources += [
      "android_qorai_referrer.cc",
      "android_qorai_referrer.h",
    ]

    deps += [ ":jni_headers" ]
  }
}

if (is_android) {
  generate_jni("jni_headers") {
    sources = [ "//qorai/android/java/org/chromium/chrome/browser/util/QoraiReferrer.java" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "qorai_referrals_service_unittest.cc" ]
  deps = [
    ":browser",
    "//base",
    "//base/test:test_support",
    "//qorai/components/qorai_referrals/common",
    "//qorai/components/constants",
    "//components/prefs:test_support",
    "//content/test:test_support",
    "//services/network:test_support",
    "//services/network/public/cpp",
    "//testing/gtest",
  ]

  if (is_mac) {
    sources += [ "file_extended_attribute_unittest.cc" ]
  }
}
