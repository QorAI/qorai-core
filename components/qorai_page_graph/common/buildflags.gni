# Copyright (c) 2022 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//qorai/build/qorai_channel.gni")

declare_args() {
  enable_qorai_page_graph =
      (is_win || is_mac || is_linux) &&
      target_os != "android"  # Do not enable PG in secondary toolchains.
}

declare_args() {
  enable_qorai_page_graph_webapi_probes =
      enable_qorai_page_graph &&
      (!is_official_build || qorai_channel == "dev" ||
       qorai_channel == "nightly")
}

# Allow Page Graph only in desktop builds.
assert(!enable_qorai_page_graph || is_win || is_mac || is_linux)

assert(!enable_qorai_page_graph_webapi_probes ||
       enable_qorai_page_graph_webapi_probes == enable_qorai_page_graph)

gn_args_page_graph_file = "$root_build_dir/gn_args_page_graph.json"

if (current_toolchain == default_toolchain) {
  write_file(gn_args_page_graph_file,
             {
               enable_qorai_page_graph = enable_qorai_page_graph
               enable_qorai_page_graph_webapi_probes =
                   enable_qorai_page_graph_webapi_probes
             },
             "json")
}
