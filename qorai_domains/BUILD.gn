# Copyright (c) 2023 The QorAI Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//build/buildflag_header.gni")
import("buildflags.gni")

source_set("qorai_domains") {
  sources = [
    "service_domains.cc",
    "service_domains.h",
  ]

  deps = [
    ":buildflags",
    "//base",
  ]

  if (is_android) {
    sources += [ "android/qorai_domains_utils_android.cc" ]
    deps += [ "//qorai/qorai_domains/android:jni_headers" ]
  }
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [
    "QORAI_SERVICES_PRODUCTION_DOMAIN=\"$qorai_services_production_domain\"",
    "QORAI_SERVICES_STAGING_DOMAIN=\"$qorai_services_staging_domain\"",
    "QORAI_SERVICES_DEV_DOMAIN=\"$qorai_services_dev_domain\"",
  ]

  # Consumers should use the qorai_domains helper functions.
  visibility = [ ":*" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "service_domains_unittest.cc" ]

  deps = [
    ":qorai_domains",
    ":buildflags",
    "//base",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
