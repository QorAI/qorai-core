# Copyright (c) 2021 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//qorai/build/ios/mojom/mojom_wrappers.gni")
import("//build/config/ios/rules.gni")
import("//ios/build/config.gni")

config("mojom_header_config") {
  visibility = [
    ":*",
    "//qorai/ios/app",
  ]
  include_dirs = [ "$target_gen_dir" ]
}

source_set("qorai_wallet") {
  configs += [ ":mojom_header_config" ]
  sources = [
    "qorai_wallet_api+private.h",
    "qorai_wallet_api.h",
    "qorai_wallet_api.mm",
    "qorai_wallet_provider_delegate_ios+private.h",
    "qorai_wallet_provider_delegate_ios.h",
    "qorai_wallet_provider_delegate_ios.mm",
    "qorai_wallet_solana_utils.h",
    "qorai_wallet_solana_utils.mm",
    "token_registry_utils.h",
    "token_registry_utils.mm",
  ]
  deps = [
    ":wallet_mojom_wrappers",
    "//base",
    "//qorai/base/apple",
    "//qorai/components/qorai_wallet/browser",
    "//qorai/components/qorai_wallet/browser:constants",
    "//qorai/components/qorai_wallet/common",
    "//qorai/components/qorai_wallet/resources:ethereum_provider_generated_resources",
    "//qorai/components/resources:static_resources_grit",
    "//qorai/ios/browser/api/url",
    "//qorai/ios/browser/qorai_wallet",
    "//ios/chrome/browser/content_settings/model",
    "//ios/chrome/browser/shared/model/paths",
    "//ios/chrome/browser/shared/model/profile",
    "//ios/web/public/thread",
    "//net",
    "//ui/base",
  ]
}

ios_objc_mojom_wrappers("wallet_mojom_wrappers") {
  mojom_target = "//qorai/components/qorai_wallet/common:mojom"
  sources = [
    "//qorai/components/qorai_wallet/common/qorai_wallet.mojom",
    "//qorai/components/qorai_wallet/common/meld_integration.mojom",
  ]
  generate_namespace = false
  exclude_types = [
    "CardanoProvider",
    "PageHandlerFactory",
    "PanelHandlerFactory",
  ]
}
