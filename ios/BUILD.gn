# Copyright (c) 2019 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.

import("//qorai/build/config.gni")
import("//qorai/build/ios/config.gni")
import("//qorai/build/ios/create_xcframework.gni")
import("//qorai/build/mac/tweak_info_plist.gni")
import("//qorai/components/ai_chat/ios/browser/headers.gni")
import("//qorai/components/qorai_shields/ios/browser/headers.gni")
import("//qorai/components/debounce/ios/browser/headers.gni")
import("//qorai/components/url_sanitizer/ios/browser/headers.gni")
import("//qorai/ios/browser/api/ads/headers.gni")
import("//qorai/ios/browser/api/qorai_domains/headers.gni")
import("//qorai/ios/browser/api/qorai_rewards/headers.gni")
import("//qorai/ios/browser/api/qorai_services_key/headers.gni")
import("//qorai/ios/browser/api/qorai_shields/headers.gni")
import("//qorai/ios/browser/api/qorai_stats/headers.gni")
import("//qorai/ios/browser/api/qorai_user_agent/headers.gni")
import("//qorai/ios/browser/api/qorai_wallet/headers.gni")
import("//qorai/ios/browser/api/certificate/headers.gni")
import("//qorai/ios/browser/api/content_settings/headers.gni")
import("//qorai/ios/browser/api/credential_provider/headers.gni")
import("//qorai/ios/browser/api/de_amp/headers.gni")
import("//qorai/ios/browser/api/developer_options_code/headers.gni")
import("//qorai/ios/browser/api/favicon/headers.gni")
import("//qorai/ios/browser/api/features/headers.gni")
import("//qorai/ios/browser/api/https_upgrade_exceptions/headers.gni")
import("//qorai/ios/browser/api/https_upgrades/headers.gni")
import("//qorai/ios/browser/api/ipfs/headers.gni")
import("//qorai/ios/browser/api/ntp_background_images/headers.gni")
import("//qorai/ios/browser/api/omnibox/headers.gni")
import("//qorai/ios/browser/api/opentabs/headers.gni")
import("//qorai/ios/browser/api/p3a/headers.gni")
import("//qorai/ios/browser/api/profile/headers.gni")
import("//qorai/ios/browser/api/qr_code/headers.gni")
import("//qorai/ios/browser/api/query_filter/headers.gni")
import("//qorai/ios/browser/api/session_restore/headers.gni")
import("//qorai/ios/browser/api/skus/headers.gni")
import("//qorai/ios/browser/api/storekit_receipt/headers.gni")
import("//qorai/ios/browser/api/translate/headers.gni")
import("//qorai/ios/browser/api/unicode/headers.gni")
import("//qorai/ios/browser/api/unzip/headers.gni")
import("//qorai/ios/browser/api/url/headers.gni")
import("//qorai/ios/browser/api/web/web_state/headers.gni")
import("//qorai/ios/browser/api/web_view/headers.gni")
import("//qorai/ios/browser/api/webcompat_reporter/headers.gni")
import("//qorai/ios/browser/qorai_shields/headers.gni")
import("//qorai/ios/browser/debounce/headers.gni")
import("//qorai/ios/browser/url_sanitizer/headers.gni")
import("//qorai/ios/web_view/headers.gni")
import("//build/apple/tweak_info_plist.gni")
import("//build/config/compiler/compiler.gni")
import("//build/config/ios/rules.gni")
import("//ios/features.gni")
import("//ios/web_view/framework/sources.gni")

# lld is required when building arm64 with optimization in order to avoid
# runtime crashes
assert(is_debug || current_cpu != "arm64" || use_lld,
       "Optimized arm64 iOS builds require lld")

config("internal_config") {
  visibility = [ ":*" ]
  ldflags =
      [ "-Wl,-rpath,/usr/lib/swift,-rpath,@executable_path/../Frameworks" ]
}

group("qorai_ios") {
  public_deps = [
    ":qorai_core_xcframework",
    ":early_partition_alloc_xcframework",
    ":generated_xcconfig",
    ":nala_assets_xcframework",
  ]
}

qorai_core_public_headers = [
  "//qorai/base/apple/base_value_bridge.h",
  "//qorai/components/ai_chat/ios/browser/ai_chat_ui_handler_bridge.h",
  "//qorai/ios/app/qorai_core_main.h",
  "//qorai/ios/app/qorai_profile_controller.h",
  "//qorai/ios/app/qorai_core_switches.h",
  "//qorai/ios/browser/ai_chat/ai_chat_utils.h",
  "//qorai/ios/browser/ai_chat/ai_chat_settings_helper.h",
  "//qorai/ios/browser/qorai_account/qorai_account_authentication_bridge.h",
  "//qorai/ios/browser/qorai_account/qorai_account_authentication_bridge_impl.h",
  "//qorai/ios/browser/qorai_news/qorai_news_pref_names_bridge.h",
  "//qorai/ios/browser/qorai_vpn/qorai_vpn_pref_names_bridge.h",
  "//qorai/ios/browser/qorai_wallet/qorai_wallet_factory_wrappers.h",
  "//qorai/ios/browser/skus/skus_sdk_factory_wrappers.h",
  "//qorai/ios/browser/api/bookmarks/qorai_bookmarks_api.h",
  "//qorai/ios/browser/api/bookmarks/qorai_bookmarks_observer.h",
  "//qorai/ios/browser/api/bookmarks/importer/qorai_bookmarks_importer.h",
  "//qorai/ios/browser/api/bookmarks/exporter/qorai_bookmarks_exporter.h",
  "//qorai/ios/browser/api/history/importer/qorai_history_importer.h",
  "//qorai/ios/browser/api/password/importer/qorai_password_importer.h",
  "//qorai/ios/browser/api/history/qorai_history_api.h",
  "//qorai/ios/browser/api/history/qorai_history_observer.h",
  "//qorai/ios/browser/api/net/certificate_utility.h",
  "//qorai/ios/browser/api/password/qorai_password_api.h",
  "//qorai/ios/browser/api/password/qorai_password_observer.h",
  "//qorai/ios/browser/api/sync/qorai_sync_api.h",
  "//qorai/ios/browser/api/sync/driver/qorai_sync_profile_service.h",
  "//qorai/ios/browser/api/web_image/web_image.h",
  "//qorai/ios/browser/keyed_service/keyed_service_factory_wrapper.h",
  "//qorai/ios/browser/api/version_info/version_info_ios.h",
  "//qorai/ios/components/prefs/pref_service_bridge.h",
  "//qorai/ios/components/prefs/pref_change_registrar_bridge.h",
  "//qorai/ios/components/constants/pref_names_bridge.h",
  "//qorai/ios/browser/safari_data_import/safari_data_import_client_delegate.h",
  "//qorai/ios/browser/safari_data_import/safari_data_importer_bridge.h",
  "//qorai/ios/browser/safari_data_import/safari_data_importer_coordinator.h",
  "//qorai/ios/browser/playlist/playlist_pref_names_bridge.h",
]

qorai_core_public_headers += ads_public_headers
qorai_core_public_headers += ai_chat_component_public_headers
qorai_core_public_headers += qorai_domains_public_headers
qorai_core_public_headers += qorai_shields_public_headers
qorai_core_public_headers += qorai_shields_api_public_headers
qorai_core_public_headers += qorai_shields_component_public_headers
qorai_core_public_headers += url_sanitizer_component_public_headers
qorai_core_public_headers += browser_url_sanitizer_public_headers
qorai_core_public_headers += browser_api_de_amp_public_headers
qorai_core_public_headers += debounce_component_public_headers
qorai_core_public_headers +=
    browser_api_https_upgrade_exceptions_service_public_headers
qorai_core_public_headers +=
    browser_api_qorai_user_agent_exceptions_ios_public_headers
qorai_core_public_headers += browser_api_https_upgrades_public_headers
qorai_core_public_headers += browser_debounce_public_headers
qorai_core_public_headers += qorai_stats_public_headers
qorai_core_public_headers += qorai_wallet_public_headers
qorai_core_public_headers += browser_api_certificate_public_headers
qorai_core_public_headers += browser_api_favicon_public_headers
qorai_core_public_headers += rewards_public_headers
qorai_core_public_headers += browser_api_omnibox_public_headers
qorai_core_public_headers += browser_api_opentabs_public_headers
qorai_core_public_headers += browser_api_qr_code_public_headers
qorai_core_public_headers += browser_api_session_restore_public_headers
qorai_core_public_headers += browser_api_query_filter_public_headers
qorai_core_public_headers += skus_public_headers
qorai_core_public_headers += browser_api_url_public_headers
qorai_core_public_headers += qorai_p3a_public_headers
qorai_core_public_headers += browser_api_web_webstate_public_headers
qorai_core_public_headers += ipfs_public_headers
qorai_core_public_headers += ntp_background_images_public_headers
qorai_core_public_headers += qorai_services_key_public_headers
qorai_core_public_headers += browser_api_features_public_headers
qorai_core_public_headers += credential_provider_public_headers
qorai_core_public_headers += developer_options_code_public_headers
qorai_core_public_headers += browser_api_storekit_receipt_public_headers
qorai_core_public_headers += content_settings_public_headers
qorai_core_public_headers += webcompat_reporter_public_headers
qorai_core_public_headers += browser_api_translate_public_headers
qorai_core_public_headers += browser_api_unicode_public_headers
qorai_core_public_headers += browser_api_unzip_public_headers
qorai_core_public_headers += qorai_web_view_public_headers
qorai_core_public_headers += browser_api_profile_public_headers
qorai_core_public_headers += qorai_ios_web_view_public_headers

action("qorai_core_umbrella_header") {
  script = "//build/config/ios/generate_umbrella_header.py"

  full_header_path = target_gen_dir + "/QoraiCore.h"
  outputs = [ full_header_path ]

  args = [
    "--output-path",
    rebase_path(full_header_path, root_build_dir),
  ]

  args += rebase_path(qorai_core_public_headers, root_build_dir)
  args += rebase_path(ios_web_view_public_headers, root_build_dir)
}

tweak_info_plist("info_plist") {
  info_plists = [ "Info.plist" ]
}

qorai_tweak_info_plist("qorai_core_plist") {
  info_plist = "$root_gen_dir/qorai/ios/info_plist_tweaked.plist"

  args = [
    "--qorai_channel=" + qorai_channel,
    "--qorai_version=" + qorai_version,
    "--skip_signing",
  ]

  deps = [ ":info_plist" ]
}

ios_framework_bundle("qorai_core_ios_framework") {
  output_name = "QoraiCore"
  output_dir = root_out_dir

  info_plist_target = ":qorai_core_plist"

  configs += [ ":internal_config" ]

  deps = [
    ":qorai_core_plist",
    ":qorai_core_umbrella_header",
    "//qorai/ios/app",
    "//qorai/ios/components/constants",
    "//qorai/ios/web_view",
  ]

  deps += ads_public_deps
  deps += ai_chat_component_public_deps
  deps += qorai_wallet_public_deps
  deps += qorai_shields_component_public_deps
  deps += rewards_public_deps
  deps += skus_public_deps
  deps += credential_provider_public_deps
  deps += webcompat_reporter_public_deps

  sources = qorai_core_public_headers
  sources += ios_web_view_public_headers

  public_headers = get_target_outputs(":qorai_core_umbrella_header")
  public_headers += qorai_core_public_headers
  public_headers += ios_web_view_public_headers
}

ios_framework_bundle("early_partition_alloc_framework") {
  output_name = "PartitionAllocSupport"
  sources = [ "//ios/chrome/app/early_pa_registration.cc" ]
  info_plist = "//ios/chrome/app/resources/EarlyPAFrameworkInfo.plist"
  extra_substitutions = [ "BUNDLE_IDENTIFIER=com.qorai.ios.$output_name" ]

  deps = [
    "//base/allocator:early_zone_registration_apple",
    "//base/allocator/partition_allocator:buildflags",
  ]
}

ios_create_xcframework("early_partition_alloc_xcframework") {
  framework_dir = "$root_build_dir/PartitionAllocSupport.framework"
  deps = [ ":early_partition_alloc_framework" ]
}

ios_create_xcframework("qorai_core_xcframework") {
  framework_dir = "$root_build_dir/QoraiCore.framework"
  remove_asset_catalogs = true
  deps = [ ":qorai_core_ios_framework" ]
}

ios_create_xcframework("nala_assets_xcframework") {
  framework_dir = "$root_build_dir/NalaAssets.framework"
  deps = [ "//qorai/ios/nala:nala_assets_framework" ]
}

# Generates an xcconfig based on GN args that are needed in the Xcode app build
# process such as Info.plist or build setting subsititions.
generated_file("generated_xcconfig") {
  outputs = [ "$root_build_dir/args.xcconfig" ]

  # Some special logic to avoid .0 patch versions in marketing versions
  ios_marketing_version = "$qorai_version_major.$qorai_version_minor"
  if (qorai_ios_marketing_version_patch != 0) {
    ios_marketing_version += ".$qorai_ios_marketing_version_patch"
  }

  contents = [
    "qorai_version_major = $qorai_version_major",
    "qorai_version_minor = $qorai_version_minor",
    "qorai_version_build = $qorai_version_build",
    "qorai_ios_marketing_version = $ios_marketing_version",
    "qorai_ios_marketing_version_patch = $qorai_ios_marketing_version_patch",
    "qorai_channel = $qorai_channel",
  ]

  # If absolute paths are stripped from debug symbols ensure that SPM/Xcode
  # also maps debug symbols the same way to allow proper debugging with LLDB
  if (strip_absolute_paths_from_debug_symbols) {
    ios_dir = rebase_path("//qorai/ios/qorai-ios")
    ios_dir_build_relative =
        rebase_path("//qorai/ios/qorai-ios", root_build_dir)
    contents += [ "qorai_ios_debug_prefix_map_flag = -debug-prefix-map $ios_dir=$ios_dir_build_relative" ]
  }

  # Set up any active compilation conditions that may be used in Swift for
  # example conditionally compiling based on `#if OFFICIAL_BUILD`
  swift_active_compliation_conditions = []
  if (is_official_build) {
    swift_active_compliation_conditions += [ "OFFICIAL_BUILD" ]
  }
  swift_active_compilation_conditions_joined =
      string_join(" ", swift_active_compliation_conditions)
  contents += [ "qorai_swift_active_compliation_conditions = $swift_active_compilation_conditions_joined" ]
}
