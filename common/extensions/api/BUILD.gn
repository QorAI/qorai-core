# Copyright (c) 2023 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//qorai/common/extensions/api/api_sources.gni")
import("//qorai/resources/qorai_grit.gni")
import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")

json_features("api_features") {
  feature_type = "APIFeature"
  method_name = "AddQoraiAPIFeatures"
  sources = [
    "_api_features.json",
    "_qorai_talk_api_features.json",
  ]
}

json_features("manifest_features") {
  feature_type = "ManifestFeature"
  method_name = "AddQoraiManifestFeatures"
  sources = [ "_manifest_features.json" ]
}

json_features("behavior_features") {
  feature_type = "BehaviorFeature"
  method_name = "AddQoraiBehaviorFeatures"
  sources = [ "_behavior_features.json" ]
}

group("extensions_features") {
  public_deps = [
    ":api_features",
    ":behavior_features",
    ":manifest_features",
    ":resources",
  ]
}

qorai_extensions_api_deps = [
  "//chrome/common/extensions/api",
  "//extensions/common/api",
  "//skia",
]

qorai_grit("resources") {
  source = "qorai_api_resources.grd"
  outputs = [
    "grit/qorai_api_resources.h",
    "qorai_api_resources.pak",
  ]
}

generated_json_strings("generated_api_json_strings") {
  sources = qorai_extensions_api_schema_sources +
            qorai_extensions_api_uncompiled_sources

  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "Qorai"
  schema_include_rules = qorai_extensions_api_schema_include_rules

  root_namespace = qorai_extensions_api_root_namespace
  deps = qorai_extensions_api_deps

  visibility = [ ":api" ]
}

generated_types("generated_api_types") {
  sources = qorai_extensions_api_schema_sources
  configs = [ "//build/config:precompiled_headers" ]
  schema_include_rules = qorai_extensions_api_schema_include_rules

  root_namespace = qorai_extensions_api_root_namespace
  deps = qorai_extensions_api_deps
  visibility = [ ":api" ]
}

# Public Targets

group("api") {
  public_deps = [
    ":generated_api_json_strings",
    ":generated_api_types",
  ]
}
