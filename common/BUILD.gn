# Copyright (c) 2017 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//qorai/build/config.gni")
import("//build/buildflag_header.gni")
import("//build/util/branding.gni")
import("//extensions/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

qorai_common_visibility_targets = [
  ":*",
  "//qorai:browser_dependencies",
  "//qorai:child_dependencies",
  "//qorai/browser/*",
  "//qorai/ios/browser/*",
  "//qorai/renderer/*",
  "//qorai/test/*",
  "//qorai/utility/*",
  "//chrome/common/*",
  "//chrome/browser/*",
  "//chrome/renderer/*",
  "//chrome/utility/*",
]

source_set("channel_info") {
  visibility = qorai_common_visibility_targets

  if (is_linux) {
    sources = [
      "qorai_channel_info_posix.cc",
      "qorai_channel_info_posix.h",
    ]

    deps = [
      "//base",
      "//components/version_info",
    ]

    configs += [ "//qorai/build/linux:linux_channel_names" ]
  }
}

group("common") {
  public_deps = [ "//chrome/common" ]
}

config("constants_configs") {
  visibility = qorai_common_visibility_targets +
               # These are layering violation and need to be fixed
               [ "//chrome/tools/build/mac:infoplist_strings_util" ]
  defines = []
  if (is_mac) {
    defines += [ "QORAI_PRODUCT_STRING=\"$chrome_product_full_name\"" ]
  }
}

mojom("mojo_bindings") {
  sources = [ "qorai_renderer_configuration.mojom" ]

  deps = [ "//mojo/public/mojom/base" ]
}
