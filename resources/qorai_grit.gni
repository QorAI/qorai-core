# Copyright (c) 2022 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//tools/grit/grit_rule.gni")

qorai_grit_resource_id_target = "//qorai/resources:qorai_resource_ids"
qorai_grit_resource_id_file =
    get_label_info(qorai_grit_resource_id_target, "target_gen_dir") +
    "/qorai_resource_ids"

template("qorai_grit") {
  grit(target_name) {
    forward_variables_from(invoker, "*")
    resource_ids = qorai_grit_resource_id_file
    if (!defined(deps)) {
      deps = []
    }
    deps += [ qorai_grit_resource_id_target ]
  }
}

template("qorai_or_default_grit") {
  if (defined(invoker.use_qorai_grit) && invoker.use_qorai_grit) {
    qorai_grit(target_name) {
      forward_variables_from(invoker, "*", TESTONLY_AND_VISIBILITY)
    }
  } else {
    grit(target_name) {
      forward_variables_from(invoker, "*", TESTONLY_AND_VISIBILITY)
    }
  }
}
