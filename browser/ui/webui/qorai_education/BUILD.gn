# Copyright (c) 2025 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

# based on //chrome/browser/ui/webui/whats_new/BUILD.gn

import("//qorai/components/qorai_education/buildflags.gni")
import("//qorai/components/qorai_vpn/common/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(enable_qorai_education)

mojom("mojo_bindings") {
  sources = [ "qorai_education.mojom" ]
  webui_module_path = "/"

  public_deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
}

source_set("qorai_education") {
  sources = [
    "qorai_education_handler.cc",
    "qorai_education_handler.h",
    "qorai_education_page_delegate_desktop.cc",
    "qorai_education_page_delegate_desktop.h",
    "qorai_education_page_ui.cc",
    "qorai_education_page_ui.h",
    "qorai_education_server_checker.cc",
    "qorai_education_server_checker.h",
  ]

  deps = [
    "//qorai/browser/resources/qorai_education:resources",
    "//qorai/browser/ui/webui/qorai_browser_command",
    "//qorai/components/qorai_vpn/common/buildflags",
    "//qorai/components/resources:strings_grit",
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/views/side_panel",
    "//chrome/browser/ui/webui:webui_util",
    "//components/language/core/browser",
    "//ui/webui",
  ]

  if (enable_qorai_vpn) {
    deps += [ "//qorai/browser/ui/qorai_vpn" ]
  }

  public_deps = [
    "//qorai/browser/ui/webui/qorai_education:mojo_bindings",
    "//qorai/components/qorai_education",
    "//qorai/ui/webui/resources/js/qorai_browser_command:mojo_bindings",
  ]
}

source_set("browser_tests") {
  testonly = true

  sources = [ "qorai_education_page_ui_browsertest.cc" ]

  deps = [
    ":qorai_education",
    "//base/test:test_support",
    "//qorai/components/qorai_education",
    "//qorai/components/qorai_vpn/common/buildflags",
    "//qorai/components/constants",
    "//chrome/browser/ui",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//content/test:test_support",
    "//testing/gtest",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "qorai_education_page_handler_unittest.cc",
    "qorai_education_server_checker_unittest.cc",
  ]

  deps = [
    ":qorai_education",
    "//base/test:test_support",
    "//qorai/browser/ui/webui/qorai_browser_command",
    "//qorai/components/qorai_education",
    "//qorai/components/qorai_vpn/common/buildflags",
    "//chrome/browser/ui",
    "//chrome/test:test_support",
    "//components/language/core/browser",
    "//testing/gtest",
  ]
}
