# Copyright (c) 2023 The QorAI Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import(
    "//qorai/browser/qorai_vpn/win/qorai_vpn_wireguard_service/allowlist.gni")
import("//qorai/build/config.gni")
import("//qorai/components/qorai_vpn/common/buildflags/buildflags.gni")
import("//build/toolchain/gcc_toolchain.gni")
import("//build/util/process_version.gni")
import("//chrome/process_version_rc_template.gni")
import("//tools/resources/generate_resource_allowlist.gni")

assert(is_win && enable_qorai_vpn)

copy("qorai_vpn_wireguard_binaries") {
  sources = [
    "//qorai/third_party/qorai-vpn-wireguard-nt-dlls/${target_cpu}/wireguard.dll",
    "//qorai/third_party/qorai-vpn-wireguard-tunnel-dlls/${target_cpu}/tunnel.dll",
  ]
  outputs = [ "$root_out_dir/{{source_file_part}}" ]
}

group("wireguard_service") {
  deps = [ ":qorai_vpn_wireguard_service" ]
  if (enable_resource_allowlist_generation) {
    deps += [ ":wireguard_resource_allowlist" ]
  }
}

generate_resource_allowlist("wireguard_resource_allowlist") {
  deps = [ ":qorai_vpn_wireguard_service" ]
  inputs = [ "$root_out_dir/qorai_vpn_wireguard_service.exe.pdb" ]
  output = wireguard_resource_allowlist_file
}

executable("qorai_vpn_wireguard_service") {
  visibility = [ ":*" ]
  sources = [
    "qorai_vpn_wireguard_service.rc",
    "qorai_wireguard_service_crash_reporter_client.cc",
    "qorai_wireguard_service_crash_reporter_client.h",
    "main.cc",
  ]

  deps = [
    ":qorai_vpn_wireguard_binaries",
    ":version_resources",
    "notifications",
    "resources",
    "service",
    "status_tray",
    "//base",
    "//base:base_static",
    "//qorai/browser/qorai_vpn/win:wireguard_utils",
    "//qorai/components/resources:strings_grit",
    "//build/win:default_exe_manifest",
    "//chrome/common:channel_info",
    "//chrome/install_static:install_static_util",
    "//components/crash/core/app",
    "//components/crash/core/app:crash_export_thunks",
    "//components/crash/core/app:run_as_crashpad_handler",
    "//components/version_info:channel",
  ]

  public_configs = [ "//build/config/win:windowed" ]
}

process_version_rc_template("version_resources") {
  sources = [ "qorai_vpn_wireguard_service.ver" ]

  output = "$target_gen_dir/qorai_vpn_wireguard_service.rc"
}

source_set("install_utils") {
  sources = [
    "install_utils.cc",
    "install_utils.h",
  ]
  public_deps = [ "//qorai/components/qorai_vpn/common/wireguard/win:qorai_wireguard_manager_idl" ]
  deps = [
    "//base",
    "//qorai/browser/qorai_vpn/win:wireguard_utils",
    "//qorai/browser/qorai_vpn/win/qorai_vpn_helper:common",
    "//qorai/browser/qorai_vpn/win/qorai_vpn_wireguard_service/service",
    "//qorai/browser/qorai_vpn/win/qorai_vpn_wireguard_service/status_tray",
    "//qorai/browser/qorai_vpn/win/qorai_vpn_wireguard_service/status_tray/status_icon:utils",
    "//qorai/components/qorai_vpn/common/win",
    "//chrome/common:channel_info",
    "//chrome/installer/util:work_item",
  ]
}

group("unit_tests") {
  testonly = true
  deps = [
    "resources:unit_tests",
    "status_tray:unit_tests",
  ]
}
