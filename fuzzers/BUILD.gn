# Copyright (c) 2022 The Qorai Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//testing/libfuzzer/fuzzer_test.gni")
import("//third_party/libprotobuf-mutator/fuzzable_proto_library.gni")

assert(use_libfuzzer)

fuzzer_test("qorai_news_parse_feed_bytes_fuzzer") {
  sources = [ "qorai_news/parse_feed_bytes_fuzzer.cc" ]
  deps = [
    "//base",
    "//qorai/components/qorai_news/browser:rust_lib",
  ]

  seed_corpus = "qorai_news/corpus/parse_feed_bytes_fuzzer/"
  dict = "//third_party/libxml/fuzz/xml.dict"
}

fuzzer_test("qorai_wallet_utils_fuzzer") {
  sources = [ "qorai_wallet/qorai_wallet_utils_fuzzer.cc" ]
  deps = [
    "//base",
    "//qorai/components/qorai_wallet/browser:utils",
  ]
}

fuzzable_proto_library("adblock_fuzzer_proto") {
  proto_in_dir = "//"
  import_dirs = [ "//testing/libfuzzer/proto" ]

  sources = [ "adblock/adblock_fuzzer.proto" ]

  deps = [ "//testing/libfuzzer/proto:url_proto" ]
}

fuzzer_test("adblock_engine_matches_fuzzer") {
  sources = [ "adblock/adblock_engine_matches_fuzzer.cc" ]

  deps = [
    ":adblock_fuzzer_proto",
    "//base",
    "//base:i18n",
    "//qorai/components/qorai_component_updater/browser",
    "//qorai/components/qorai_shields/core/browser/adblock",
    "//testing/libfuzzer/proto:url_proto",
    "//testing/libfuzzer/proto:url_proto_converter",
    "//third_party/libprotobuf-mutator",
  ]
}

fuzzer_test("adblock_engine_useresources_fuzzer") {
  sources = [ "adblock/adblock_engine_useresources_fuzzer.cc" ]
  deps = [
    "//base",
    "//qorai/components/qorai_shields/core/browser/adblock",
  ]

  dict = "//testing/libfuzzer/fuzzers/dicts/json.dict"
}

fuzzer_test("speedreader_rewriter_fuzzer") {
  sources = [ "speedreader/rewriter_fuzzer.cc" ]
  deps = [
    "//base",
    "//qorai/components/speedreader/rust/ffi",
  ]

  seed_corpus = "speedreader/corpus/rewriter_fuzzer"
  dict = "//third_party/libxml/src/fuzz/html.dict"
}

group("qorai_fuzzers") {
  testonly = true

  deps = [
    ":adblock_engine_matches_fuzzer",
    ":adblock_engine_useresources_fuzzer",
    ":qorai_news_parse_feed_bytes_fuzzer",
    ":qorai_wallet_utils_fuzzer",
    ":speedreader_rewriter_fuzzer",
  ]
}
